import com.github.spotbugs.snom.Confidence

plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    alias(libs.plugins.spotbugs)
    alias(libs.plugins.ben.manes.versions)
    alias(libs.plugins.nebula.lint)
    alias(libs.plugins.sonarqube)
    alias(libs.plugins.jacocolog)
    alias(libs.plugins.jgitver)
}

spotbugs {
    reportLevel = Confidence.valueOf('HIGH')
}

tasks.spotbugsMain {
    reports.create('html') {
        required = true
        setStylesheet('fancy-hist.xsl')
    }
}

sonar {
    properties {
        property 'sonar.projectKey', property('sonar.projectKey')
        property 'sonar.organization', property('sonar.organization')
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.branch.name', 'develop'
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
    }
}

allprojects {
    group = 'com.ak'
    apply plugin: 'idea'
    idea {
        module {
            inheritOutputDirs = true
            downloadJavadoc = true
            downloadSources = true
        }
    }

    sourceSets {
        main.output.resourcesDir = main.java.classesDirectory
    }

    apply plugin: 'jacoco'
    apply plugin: 'org.barfuin.gradle.jacocolog'

    jacoco {
        toolVersion = '0.8.13'
    }

    jacocoTestReport {
        reports {
            xml.required = true
        }
    }
}

subprojects {
    apply plugin: 'java'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(23)
        }
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
    }

    compileTestJava {
        options.encoding = 'UTF-8'
    }

    test {
        forkEvery = 5
        useJUnitPlatform()
        testLogging {
            events 'failed'
        }
        maxHeapSize = '1G'
    }
}