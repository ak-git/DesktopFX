import com.github.spotbugs.snom.Confidence

plugins {
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'java'
    id 'java-library'
    id 'idea'
    id 'org.sonarqube' version '4.4.1.3373'
    id 'jacoco'

    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.14'

    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'com.github.spotbugs' version '5.1.2'

    id 'com.github.ben-manes.versions' version '0.49.0'
    id 'nebula.lint' version '18.1.0'
}

spotbugsMain {
    reports {
        html {
            stylesheet = 'fancy-hist.xsl'
        }
    }
    reportLevel = Confidence.HIGH
}

sonar {
    properties {
        property 'sonar.projectKey', 'ak-git_DesktopFX'
        property 'sonar.organization', 'ak-git'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.branch.name', 'develop'
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
    }
}

ext {
    junit = 'org.junit:junit-bom:5.10.0'
    jupiter = 'org.junit.jupiter:junit-jupiter:5.10.0'
    assertj = 'org.assertj:assertj-core:3.24.2'
    jsr305 = 'com.google.code.findbugs:jsr305:3.0.2'
    javax_inject = 'jakarta.inject:jakarta.inject-api:2.0.1'
    uom_se = 'tec.uom:uom-se:1.0.10'
    javax_json = 'org.glassfish:javax.json:1.1.4'
    commons_math3 = 'org.apache.commons:commons-math3:3.6.1'
    commons_csv = 'org.apache.commons:commons-csv:1.10.0'
    jSerialComm = 'com.fazecast:jSerialComm:[2.9.3,3.0.0)'
    jenetics = 'io.jenetics:jenetics:7.2.0'
    javaVersion = JavaVersion.VERSION_20
    javaFxVersion = '20'
}

wrapper {
    description 'Setup the Gradle wrapper'
    gradleVersion = '8.4'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

allprojects {
    group = 'com.ak'
    version = '2023.11.03'
    apply plugin: 'idea'
    idea {
        module {
            inheritOutputDirs = true
        }
    }

    sourceSets {
        main.output.resourcesDir = main.java.classesDirectory
    }

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.javamodularity.moduleplugin'
    apply plugin: 'jacoco'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
    }

    jacoco {
        toolVersion = '0.8.9'
    }

    jacocoTestReport {
        reports {
            xml.required = true
        }
    }

    dependencies {
        implementation jsr305
        testImplementation(platform(junit))
        testImplementation jupiter
        testImplementation assertj
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'started', 'passed', 'skipped', 'failed'
        }
        forkEvery = 5
        maxHeapSize = '1G'
    }
}

dependencies {
    implementation project(':FxCore')
    implementation project(':Communication')

    implementation javax_inject
    implementation uom_se
    implementation javax_json
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation commons_csv

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

javafx {
    version = javaFxVersion
    modules = ['javafx.controls', 'javafx.fxml']
}

configurations {
    configureEach {
        exclude group: 'jakarta.annotation', module: 'jakarta.annotation-api'
        exclude group: 'aopalliance', module: 'aopalliance'
    }
}

processResources {
    filesMatching('**/keys*.properties') {
        expand(project.properties)
    }
}

compileJava.inputs.files(processResources)

application {
    mainModule = 'com.ak.fx.desktop'
    mainClass = 'com.ak.fx.desktop.SpringFxApplication'
}