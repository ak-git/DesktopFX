plugins {
    id 'java-library'
}

def jsr305 = 'com.google.code.findbugs:jsr305:3.0.2'
def testNG = 'org.testng:testng:7.0.0'
def uom_se = 'tec.uom:uom-se:1.0.10'
def javax_json = 'org.glassfish:javax.json:1.1.4'
def commons_math3 = 'org.apache.commons:commons-math3:3.6.1'
def javax_inject = 'javax.inject:javax.inject:1'
def jssc = 'org.scream3r:jssc:2.8.0'
def spring_context = 'org.springframework:spring-context:5.2.0.RELEASE'

ext.projectIds = ['java': JavaVersion.VERSION_1_10]

wrapper {
    description 'Setup the Gradle wrapper'
    gradleVersion = "5.6.3"
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

allprojects {
    group = 'com.ak'
    version = '2019.4'
    apply plugin: 'idea'
}

subprojects {
    apply plugin: 'java-library'
    sourceCompatibility = projectIds.java
    targetCompatibility = projectIds.java

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = 'Apache Repository'
            url = 'https://repository.apache.org/content/repositories/snapshots/'
        }
        maven {
            name = 'Spring rich client repository'
            url = 'http://spring-rich-c.sourceforge.net/maven2repository'
        }
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
        jcenter()
    }

    dependencies {
        implementation jsr305
        testImplementation testNG
    }

    test {
        forkEvery = 5
        useTestNG()
        maxHeapSize = '1G'
    }

    test.useTestNG()
}

project(':Util') {
    dependencies {
        implementation uom_se
        implementation javax_json
        implementation commons_math3
    }
}

project(':Communication') {
    dependencies {
        api project(':Util')
        api javax_inject
        implementation uom_se
        implementation jssc
    }
}

project(':FxCore') {
    dependencies {
        api project(':Util')
        api spring_context
    }
}

project(':FxDesktop') {
    apply plugin: 'application'
    mainClassName = 'com.ak.fx.desktop.FxApplication'

    run {
        jvmArgs '-server', '-Xms512m', '-Xmx1024m'
    }

    dependencies {
        implementation project(':FxCore')
        implementation project(':Communication')
        implementation spring_context
        implementation javax_inject
        implementation uom_se
        implementation javax_json
    }

    processResources {
        from(sourceSets.main.resources.srcDirs) {
            exclude '**/*.properties'
        }
        from(sourceSets.main.resources.srcDirs) {
            include '**/*.properties'
            filter { String line ->
                line.replaceAll('application.version', project.version.toString())
            }
        }
    }
}