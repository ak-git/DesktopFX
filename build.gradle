plugins {
    id "org.javamodularity.moduleplugin" version "1.5.0"
    id 'java-library'
    id 'idea'
    id "org.sonarqube" version "2.8"
    id 'jacoco'

    id "application"
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id "org.beryx.jlink" version "2.17.3"
}

sonarqube {
    properties {
        property "sonar.projectKey", "ak-git_DesktopFX"
        property "sonar.organization", "ak-git"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", "ec97b4884454351da027c6345043fc8ae9401d53"
        property "sonar.branch.name", "briko"
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml'
    }
}

jacoco {
    toolVersion = "0.8.5"
}

ext.jsr305 = 'com.google.code.findbugs:jsr305:3.0.2'
ext.testNG = 'org.testng:testng:7.0.0'
ext.uom_se = 'tec.uom:uom-se:1.0.10'
ext.javax_json = 'org.glassfish:javax.json:1.1.4'
ext.commons_math3 = 'org.apache.commons:commons-math3:3.6.1'
ext.javax_inject = 'javax.inject:javax.inject:1'
ext.jssc = 'org.scream3r:jssc:2.8.0'
ext.spring_context = 'org.springframework:spring-context:5.2.4.RELEASE'
ext.jenetics = 'io.jenetics:jenetics:5.2.0'
ext.projectIds = ['java': JavaVersion.VERSION_13]

wrapper {
    description 'Setup the Gradle wrapper'
    gradleVersion = "6.2.2"
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

allprojects {
    group = 'com.ak'
    version = '2020.03'
    apply plugin: 'idea'
    idea {
        module {
            inheritOutputDirs = true
        }
    }

    sourceSets {
        main.output.resourcesDir = main.java.outputDir
    }

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.javamodularity.moduleplugin'
    apply plugin: 'jacoco'

    sourceCompatibility = projectIds.java
    targetCompatibility = projectIds.java

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
        jcenter()
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    dependencies {
        implementation jsr305
        implementation testNG
    }

    test {
        forkEvery = 5
        useTestNG()
        maxHeapSize = '1G'
    }
}

dependencies {
    implementation project(':FxCore')
    implementation project(':Communication')

    implementation javax_inject
    implementation uom_se
    implementation javax_json
    implementation 'org.beryx:text-io:3.4.0'
}

javafx {
    version = '13.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

mainClassName = "$moduleName/com.ak.fx.desktop.R3Output"

jlink {
    options = ['--bind-services', '--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'com.ak.fx.desktop'
    }
}