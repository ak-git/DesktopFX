plugins {
    id 'java-library'
}

ext.projectIds = ['java': JavaVersion.VERSION_1_10]

wrapper {
    description 'Setup the Gradle wrapper'
    gradleVersion = "6.0.1"
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

allprojects {
    group = 'com.ak'
    version = '2019.3'
    apply plugin: 'idea'
}

subprojects {
    apply plugin: 'java-library'
    sourceCompatibility = projectIds.java
    targetCompatibility = projectIds.java

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = 'Apache Repository'
            url = 'https://repository.apache.org/content/repositories/snapshots/'
        }
        maven {
            name = 'Spring rich client repository'
            url = 'http://spring-rich-c.sourceforge.net/maven2repository'
        }
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
        jcenter()
    }

    dependencies {
        implementation 'com.google.code.findbugs:jsr305:3.0.2'
        testImplementation 'org.testng:testng:7.0.0-beta7'
    }

    test {
        forkEvery = 5
        useTestNG()
        maxHeapSize = '1G'
    }

    test.useTestNG()
}

project(':Util') {
    dependencies {
        api 'tec.uom:uom-se:1.0.10'
        api 'org.glassfish:javax.json:1.1.4'
        implementation 'org.apache.commons:commons-math3:3.6.1'
    }
}

project(':Communication') {
    dependencies {
        api project(':Util')
        api 'javax.inject:javax.inject:1'
        implementation 'org.scream3r:jssc:2.8.0'
    }
}

project(':FxCore') {
    dependencies {
        api project(':Util')
        api 'org.springframework:spring-context:5.1.9.RELEASE'
    }
}

project(':FxDesktop') {
    apply plugin: 'application'
    mainClassName = 'com.ak.fx.desktop.FxApplication'

    run {
        jvmArgs '-server', '-Xms512m', '-Xmx1024m'
    }

    dependencies {
        implementation project(':FxCore')
        implementation project(':Communication')
    }
}