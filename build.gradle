plugins {
    id "org.javamodularity.moduleplugin" version "1.7.0"
    id 'java'
    id 'java-library'
    id 'idea'
    id "org.sonarqube" version "3.1.1"
    id 'jacoco'

    id "application"
    id 'org.openjfx.javafxplugin' version '0.0.9'

    id 'org.springframework.boot' version '2.4.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "com.github.spotbugs" version "4.7.0"
}

spotbugs {
    reportLevel = 'high'
}

spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "ak-git_DesktopFX"
        property "sonar.organization", "ak-git"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", "ec97b4884454351da027c6345043fc8ae9401d53"
        property "sonar.branch.name", "develop"
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml'
    }
}

jacoco {
    toolVersion = "0.8.6"
}

ext {
    testNG = 'org.testng:testng:7.4.0'
    jsr305 = 'com.google.code.findbugs:jsr305:3.0.2'
    javax_inject = 'javax.inject:javax.inject:1'
    uom_se = 'tec.uom:uom-se:1.0.10'
    javax_json = 'org.glassfish:javax.json:1.1.4'
    commons_math3 = 'org.apache.commons:commons-math3:3.6.1'
    commons_csv = 'org.apache.commons:commons-csv:1.8'
    jSerialComm = 'com.fazecast:jSerialComm:[2.0.0,3.0.0)'
    javaVersion = JavaVersion.VERSION_15
}

wrapper {
    description 'Setup the Gradle wrapper'
    gradleVersion = "6.8.3"
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

allprojects {
    group = 'com.ak'
    version = '2021.03.24'
    apply plugin: 'idea'
    idea {
        module {
            inheritOutputDirs = true
        }
    }

    sourceSets {
        main.output.resourcesDir = main.java.outputDir
    }

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.javamodularity.moduleplugin'
    apply plugin: 'jacoco'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
        jcenter()
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    dependencies {
        implementation jsr305
        implementation testNG
    }

    test {
        forkEvery = 5
        useTestNG()
        maxHeapSize = '1G'
    }
}

dependencies {
    implementation project(':FxCore')
    implementation project(':Communication')

    implementation javax_inject
    implementation uom_se
    implementation javax_json
    implementation 'org.springframework.boot:spring-boot-starter'
}

javafx {
    version = '15'
    modules = ['javafx.controls', 'javafx.fxml']
}

configurations {
    all {
        exclude group: 'jakarta.annotation', module: 'jakarta.annotation-api'
        exclude group: 'aopalliance', module: 'aopalliance'
    }
}

processResources {
    filesMatching("**/keys*.properties") {
        expand(project.properties)
    }
}

mainClassName = "com.ak.fx.desktop.SpringFxApplication"