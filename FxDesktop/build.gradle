plugins {
    id "application"
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id "org.beryx.jlink" version "2.16.4"
}

dependencies {
    implementation project(':FxCore')
    implementation project(':Communication')

    implementation spring_context
    implementation javax_inject
    implementation uom_se
    implementation javax_json
}

javafx {
    version = '13.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

mainClassName = "com.ak.fx.desktop/com.ak.fx.desktop.FxApplication"

jlink {
    imageZip = file("$buildDir/image-zip/hellofx.zip")
    options = ['--bind-services', '--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    if(System.getenv("CI")) {
        ["win", "linux", "mac"].each { name ->
            targetPlatform(name) {
                jdkHome = System.getenv("JDK_HOME_${name.toUpperCase()}")
                addExtraModulePath(System.getenv("OPENJFX_MODS_${name.toUpperCase()}"))
            }
        }
    }

    launcher {
        name = 'com.ak.fx.desktop'
    }
}